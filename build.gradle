buildscript {
	apply from: 'dependencies.gradle'
}

plugins {
	id 'org.springframework.boot' version '2.7.3' apply false
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'

    id 'java-library'
    id 'maven-publish'

    id 'org.barfuin.gradle.jacocolog' version '1.0.1'
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

group = 'ee.alekal'
version = '2.1.5'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation          deps.spring
	compileOnly             deps.lombok
	annotationProcessor     deps.lombok

    testImplementation      deps.spring_test
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact "build/libs/bowlingscore-${version}.jar"
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    outputs.upToDateWhen {false}
    testLogging {
        events "PASSED", "SKIPPED", "FAILED"
    }
}

jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "ee/alekal/bowlingscore/dto/*",
                    "ee/alekal/bowlingscore/controller/*",
                    "ee/alekal/bowlingscore/internal/handler/*",
                    "**/config/*"
            ])
        }))
    }
}
